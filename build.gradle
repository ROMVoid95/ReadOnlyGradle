plugins {
    id 'idea'
	id 'groovy'
    id 'eclipse'
	id 'java-gradle-plugin'
	id 'maven-publish'
	id 'java-library'
	id 'com.gradle.plugin-publish' version '0.12.0'
	id 'com.gradle.build-scan' version '1.16'
}

ext {
	inceptionYear = '2020'
	url = 'https://github.com/ROMVoid95/ReadOnlyGradle'
}

group = 'net.rom.romgradle'
version = '1.0.4'
description = 'Gradle plugin apply ForgeGradle, CurseGradle and Galacticraft Dependencies'
archivesBaseName = 'ReadOnlyGradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
	shade
	compileOnly.extendsFrom shade
}

repositories {
	jcenter()
	maven {
		url "https://plugins.gradle.org/m2/"
	}
	maven {
		name = "forge"
		url = "http://files.minecraftforge.net/maven"
	}
}

dependencies {
    compile gradleApi()
	compile localGroovy()
    compile 'com.google.guava:guava:23.0'
    shade 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    shade 'gradle.plugin.com.matthewprenger:CurseGradle:1.4.0'

    // Use JUnit test framework
    testCompile 'junit:junit:4.12'
}

jar {
	configurations.shade.each { dep ->
		/* I can use this again to find where dupes come from, so.. gunna just keep it here.
		logger.lifecycle(dep.toString())
		project.zipTree(dep).visit {
			element ->
				def path = element.relativePath.toString()
				if (path.contains('org/eclipse/core') && path.endsWith('.class'))
					println "  $element.relativePath"
			
		}
		*/
		from(project.zipTree(dep)){
			exclude 'META-INF', 'META-INF/**', '.api_description', '.options', 'about.html', 'module-info.class', 'plugin.properties', 'plugin.xml', 'about_files/**'
			duplicatesStrategy 'warn'
		}
	}

	manifest {
		attributes 'version':project.version
		attributes 'javaCompliance': project.targetCompatibility
		attributes 'group':project.group
		attributes 'Implementation-Version': project.version
	}
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

artifacts {
	archives jar
	archives sourcesJar
}

assemble.dependsOn sourcesJar

[compileJava, compileGroovy].each {
	it.options.encoding = 'UTF-8'
	it.options.deprecation = true
	it.options.fork = true
}

pluginBundle {
	website = project.url
	vcsUrl = 'https://github.com/ROMVoid95/ReadOnlyGradle'
	plugins {
		readOnlyGradlePlugin {
			id = project.group
			description = project.description
			displayName = 'ReadOnlyGradle'
			version = project.version
			tags = ['minecraftforge', 'curseforge', 'minecraft', 'forge', 'galacticraft']
		}
	}
}

buildScan {
	termsOfServiceUrl = 'https://gradle.com/terms-of-service'
	termsOfServiceAgree = 'yes'
}